# check if "sub" (file or dir) is below "dir"
function(sv_is_subdir res dir sub )
  get_filename_component(dir "${dir}" ABSOLUTE)
  get_filename_component(sub "${sub}" ABSOLUTE)
  file(TO_CMAKE_PATH "${dir}" dir)
  file(TO_CMAKE_PATH "${sub}" sub)
  set(dir "${dir}/")
  string(LENGTH "${dir}" len)
  string(LENGTH "${sub}" len_sub)
  if(NOT len GREATER len_sub)
    string(SUBSTRING "${sub}" 0 ${len} prefix)
  endif()
  if(prefix AND prefix STREQUAL dir)
    set(${res} TRUE PARENT_SCOPE)
  else()
    set(${res} FALSE PARENT_SCOPE)
  endif()
endfunction()

function(sv_is_opencv_directory result_var dir)
  set(result FALSE)
  foreach(parent ${LibSV_SOURCE_DIR} ${LibSV_BINARY_DIR})
    sv_is_subdir(result "${parent}" "${dir}")
    if(result)
      break()
    endif()
  endforeach()
  set(${result_var} ${result} PARENT_SCOPE)
endfunction()

# adds include directories in such a way that directories from the OpenCV source tree go first
function(sv_include_directories)
  set(__add_before "")
  foreach(dir ${ARGN})
    sv_is_opencv_directory(__is_libsv_dir "${dir}")
    if(__is_libsv_dir)
      list(APPEND __add_before "${dir}")
    elseif(SV_GCC AND NOT CMAKE_CXX_COMPILER_VERSION VERSION_LESS "6.0" AND
           dir MATCHES "/usr/include$")
      # workaround for GCC 6.x bug
    else()
      include_directories(AFTER SYSTEM "${dir}")
    endif()
  endforeach()
  include_directories(BEFORE ${__add_before})
endfunction()

macro(sv_debug_message)
    string(REPLACE ";" " " __msg "${ARGN}")
    message(STATUS "${__msg}")
endmacro()